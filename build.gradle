buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'idea'
    id 'maven-publish'
}

//The Plugin block only works for plugins in the gradle maven
apply plugin: 'net.minecraftforge.gradle'

version = "${MC_VERSION}-${MOD_VERSION}"
group = 'net.dark_roleplay'
archivesBaseName = 'travellers_map'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

configurations {
    apiCompile.extendsFrom(compile)
}

sourceSets {
    api
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

minecraft {
    mappings channel: 'snapshot', version: "${MAPPINGS_VERSION}"

    runs {
        client {
            workingDirectory project.file('runs/client')
            property 'forge.logging.console.level', 'debug'

            environment 'MOD_CLASSES',
                    "archivesBaseName%%${project.file('out/production/resources').canonicalPath};" +
                    "archivesBaseName%%${project.file('out/production/classes').canonicalPath};" +
                    "archivesBaseName%%${project.file('out/api/resources').canonicalPath};"+
                    "archivesBaseName%%${project.file('out/api/classes').canonicalPath}"
        }

        server {
            workingDirectory project.file('runs/server')
            property 'forge.logging.console.level', 'debug'

            environment 'MOD_CLASSES',
                    "archivesBaseName%%${project.file('out/production/resources').canonicalPath};" +
                    "archivesBaseName%%${project.file('out/production/classes').canonicalPath};" +
                    "archivesBaseName%%${project.file('out/api/resources').canonicalPath};"+
                    "archivesBaseName%%${project.file('out/api/classes').canonicalPath}"
        }
    }
}

dependencies {
    minecraft([
        group   : 'net.minecraftforge',
        name    : 'forge',
        version : "${MC_VERSION}-${FORGE_VERSION}"
    ])
}

jar {
    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.api.output.resourcesDir
    from sourceSets.api.output.classesDirs

    manifest {
        attributes([
            "Specification-Title": 'travellers_map',
            "Specification-Vendor": 'Dark Roleplay',
            "Specification-Version": '1', // We are version 1 of ourselves
            "Implementation-Title": 'Travellers Map',
            "Implementation-Version": "${version}",
            "Implementation-Vendor" : 'Travellers Map',
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.api.allSource
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.api.output
}

artifacts {
    archives apiJar
    archives sourcesJar
    archives javadocJar
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

idea {
    module {
        for (String excludeDirName in ["run", "out", "runs", ".github", ".idea", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}

curseforge {
    apiKey = System.getenv('CURSE_API_KEY') // This should really be in a gradle.properties file
    project {
        id = '360748'
        changelog = file('changelog.txt') // A file can also be set using: changelog = file('changelog.txt')
        releaseType = 'beta'
        mainArtifact(jar) {
            displayName = "Travellers Map - ${MC_VERSION} - v${MOD_VERSION}"
        }
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact reobfArtifact
            artifact apiJar
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url System.getenv("MVN_REPO")
            credentials {
                username System.getenv("MVN_USER")
                password System.getenv("MVN_PASSWORD")
            }
        }
    }
}